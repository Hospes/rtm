plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.appdistribution'
    id 'dagger.hilt.android.plugin'
}

def buildNumber = project.hasProperty("build.number") ? project.ext["build.number"] : "dev"

android {
    compileSdkVersion project.compileSdk

    defaultConfig {
        applicationId "ua.hospes.rtm"
        minSdkVersion project.minSdk
        targetSdkVersion project.targetSdk

        versionCode 9
        versionName "2.0.$buildNumber"

        archivesBaseName = "rtm-v$versionName"

        multiDexEnabled true

        buildConfigField 'String', 'NOTIFICATION_CHANNEL_RACE', "\"Race progress\""

        javaCompileOptions {
            annotationProcessorOptions {
                // arguments += ["room.incremental": "true"]
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('../release.keystore')
            storePassword 'sync753df'
            keyAlias 'rtm'
            keyPassword '>fFRteQE6,^uDQ]a'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "d"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                serviceCredentialsFile = "racing-time-manager-ad1811bea75d.json"
                releaseNotesFile = "release-notes"
            }
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        useIR = true
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlinx.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${kotlinx.coroutines}"

    def compose = compose_version
    implementation "androidx.compose.ui:ui:$compose"
    implementation "androidx.compose.ui:ui-util:$compose"
    implementation "androidx.compose.ui:ui-tooling:$compose"
    implementation "androidx.compose.foundation:foundation:$compose"
    implementation "androidx.compose.foundation:foundation-layout:$compose"
    implementation "androidx.compose.runtime:runtime:$compose"
    implementation "androidx.compose.animation:animation:$compose"
    implementation "androidx.compose.material:material:$compose"
    implementation "androidx.compose.material:material-icons-core:$compose"
    implementation "androidx.compose.material:material-icons-extended:$compose"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'
    implementation 'androidx.navigation:navigation-compose:2.4.0-alpha05'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.0-beta01'

    def lifecycle = '2.4.0-alpha02'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle"

    implementation "androidx.core:core-ktx:${androidx.core}"
    implementation 'androidx.activity:activity-compose:1.3.0'
    implementation "androidx.fragment:fragment-ktx:${androidx.fragment}"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.appcompat:appcompat:${androidx.appcompat}"
    implementation "androidx.constraintlayout:constraintlayout:${androidx.constraint}"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.google.android.material:material:1.4.0'

    // logging
    implementation "com.jakewharton.timber:timber:${versions.timber}"

    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.4.7'

    def room = '2.3.0'
    implementation "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$room"
    implementation "androidx.room:room-ktx:$room"

    //implementation "ua.hospes.undobutton:undobutton:0.2.0"

    implementation "com.google.dagger:hilt-android:${google.hilt}"
    kapt "com.google.dagger:hilt-compiler:${google.hilt}"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha03'

    implementation 'net.sourceforge.jexcelapi:jxl:2.6.12'

    implementation 'com.google.firebase:firebase-crashlytics:18.2.0'
    implementation 'com.google.firebase:firebase-analytics:19.0.0'

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$coreDesugaring"
}

kapt { correctErrorTypes true }

apply plugin: 'com.google.gms.google-services'